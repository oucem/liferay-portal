import com.liferay.gradle.plugins.node.tasks.DownloadNodeModuleTask
import com.liferay.gradle.plugins.node.tasks.ExecuteNodeTask

dependencies {
	compile group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "2.4.1"
	compile group: "com.liferay.portal", name: "portal-impl", version: liferay.portalVersion
	compile group: "com.liferay.portal", name: "portal-service", version: liferay.portalVersion
	compile group: "com.liferay.portal", name: "util-taglib", version: liferay.portalVersion
	compile group: "javax.portlet", name: "portlet-api", version: "2.0"
	compile group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compile group: "javax.servlet.jsp", name: "jsp-api", version: "2.1"
	compile group: "org.osgi", name: "org.osgi.compendium", version: "5.0.0"
}

ext {
	bowerVersion = "1.5.2"
}

liferay {
	deployDir = file("${liferayHome}/osgi/modules")
}

task downloadBower(type: DownloadNodeModuleTask)
task bowerInstall(type: ExecuteNodeTask)
task dragulaInstall(type: Copy)

bowerInstall {
	args = [new File(node.nodeDir, "node_modules/bower/bin/bower"), "install", "--allow-root"]
	dependsOn downloadBower
}

configJSModules {
	dependsOn dragulaInstall
	include "**/*.js"
	sourceDir "classes/META-INF/resources"
}

downloadBower {
	moduleName = "bower"
	moduleVersion = bowerVersion
}

dragulaInstall {
	dependsOn bowerInstall

	from new File("bower_components/dragula.js/dist/dragula.min.js")
	into file("classes/META-INF/resources")
}